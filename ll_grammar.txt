Start       : Stmts;

Stmts       : Stmt Stmts';
Stmts'      : Stmt Stmts';
Stmts'      : e;

Stmt        : PRINT Expr;
Stmt        : NameStmt;

NameStmt    : NAME NameStmt';
NameStmt'   : Call';
NameStmt'   : AssignStmt;

AssignStmt  : EQUAL Assignable;
Assignable  : Expr;
Assignable  : Function;

Function    : FUNC LPAREN ParamDecl LCURLY FuncBody RCURLY;
ParamDecl   : RPAREN;
ParamDecl   : DParams RPAREN;
FuncBody    : Return;
FuncBody    : Stmts Return;

Return      : RETURN RetExpr;
RetExpr     : Expr;
RetExpr     : e;

Expr        : AddSub;

AddSub      : MulDiv AddSub';
AddSub'     : PLUS MulDiv AddSub';
AddSub'     : DASH MulDiv AddSub';
AddSub'     : e;

MulDiv      : Atomic MulDiv';
MulDiv'     : STAR Atomic MulDiv';
MulDiv'     : SLASH Atomic MulDiv';
MulDiv'     : e;

Atomic      : Value;
Atomic      : LPAREN Expr RPAREN;

Value       : INT_VAL;
Value       : NameOrCall;

NameOrCall  : NAME NameOrCall';
NameOrCall' : Call';
NameOrCall' : e;

Call'       : LPAREN Call'';
Call''      : RPAREN;
Call''      : Params RPAREN;

Params      : Expr Params';
Params'     : COMMA Expr Params';
Params'     : e;

DParams     : NAME DParams';
DParams'    : COMMA NAME DParams';
